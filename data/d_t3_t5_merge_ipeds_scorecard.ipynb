{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "0c6f955c-bd9b-46a4-ac70-572547e85420",
   "metadata": {},
   "source": [
    "## Predatory Inclusion in Non-Profit and For-Profit Online Education\n",
    "### By Christian Michael Smith, Amber D. Villalobos (equal authors), Laura T. Hamilton, and Charlie Eaton\n",
    "\n",
    "### Code by Christian Smith\n",
    "\n",
    "### Merge Data for Tables 3 and 5"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "7eb5cbf0-c148-41a3-9964-10fdb7e0ca30",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      ". qui {\n",
      "\n",
      ". qui {\n",
      "\n",
      ". save d_t3_t5_ipeds_scorecard, replace\n",
      "(file d_t3_t5_ipeds_scorecard.dta not found)\n",
      "file d_t3_t5_ipeds_scorecard.dta saved\n"
     ]
    }
   ],
   "source": [
    "***(IX) PREPARE DATA ON LOAN PROGRESS OUTCOMES FROM SCORECARD***\n",
    "\n",
    "qui {\n",
    "clear\n",
    "insheet using MERGED2018_19_PP.csv\n",
    "keep unitid bbrr* dbrr*\n",
    "gen year=2013\n",
    "save repay2013.dta, replace\n",
    "\t}\n",
    "\n",
    "\n",
    "\n",
    "**********(B) PERFORM FINAL MERGE**********\t\n",
    "\n",
    "qui {\n",
    "use hd2012a, clear\n",
    "gen year=2012\n",
    "forvalues year=2013/2020 {\n",
    "\tappend using hd`year'a\n",
    "    replace year=`year' if year==.\n",
    "\t\t}\n",
    "\t\n",
    "forvalues year=2012/2020 {\n",
    "    merge 1:1 unitid year using ef`year'a_dist, nogen update\n",
    "    merge 1:1 unitid year using first`year', nogen keepusing(f*) update\n",
    "    merge 1:1 unitid year using under`year', nogen keepusing(u*) update\n",
    "\tmerge 1:1 unitid year using trad`year', nogen keepusing(t*) update\n",
    "\tmerge 1:1 unitid year using adm`year', nogen update\n",
    "\t\t}\t\n",
    "\t\n",
    "forvalues year=2012/2019 {\n",
    "    merge 1:1 unitid year using efia`year',  nogen update\n",
    "\tmerge 1:1 unitid year using sfa`year', nogen update\n",
    "\tmerge 1:1 unitid year using ret`year', nogen update\n",
    "\t\t}\n",
    "\n",
    "forvalues year=2012/2014 {\n",
    "\tmerge 1:1 unitid year using gr`year', nogen update\n",
    "\t\t}\t\n",
    "\n",
    "merge 1:1 unitid year using repay2013, nogen update\n",
    "\n",
    "\t}\n",
    "\n",
    "\n",
    "\n",
    "**********(C) CREATE / CLEAN VARIABLES AND SAVE ANALYTIC DATASET**********\t\n",
    "\n",
    "*Miscellaneous data-cleaning and variable construction\n",
    "qui {\n",
    "gen forprofit = (control==3)\n",
    "gen nonprofit = (control!=3)\n",
    "gen allcontrols = 1\n",
    "foreach var in forprofit nonprofit {\n",
    "\treplace `var' = . if control==.\n",
    "\t\t}\n",
    "label define forprofit_lab 0 \"Nonprofit (public or private)\" 1 \"For-profit\"\n",
    "label values forprofit forprofit_lab\n",
    "label define nonprofit_lab 0 \"For-profit\" 1 \"Nonprofit (public or private)\" \n",
    "label values nonprofit nonprofit_lab\n",
    "gen blackpct = fefbkaat / feftotlt * 100\n",
    "gen latinxpct = fefhispt / feftotlt * 100\n",
    "gen aianpct = fefaiant / feftotlt * 100\n",
    "gen asiapct = fefasiat / feftotlt * 100\n",
    "gen blackpct_u = uefbkaat / ueftotlt * 100\n",
    "gen latinxpct_u = uefhispt / ueftotlt * 100\n",
    "gen aianpct_u = uefaiant / ueftotlt * 100\n",
    "gen asiapct_u = uefasiat / ueftotlt * 100\n",
    "gen malepct_u = ueftotlm / ueftotlt * 100\n",
    "gen blackpct_t = tefbkaat / teftotlt * 100\n",
    "gen latinxpct_t = tefhispt / teftotlt * 100\n",
    "gen aianpct_t = tefaiant / teftotlt * 100\n",
    "gen asiapct_t = tefasiat / teftotlt * 100\n",
    "gen malepct_t = teftotlm / teftotlt * 100\n",
    "replace hbcu = (hbcu==1)\n",
    "replace locale = . if locale==-3\n",
    "replace hdegofr1  = . if hdegofr1 ==-3\n",
    "sort unitid year\n",
    "gen pctonline = efdeexc / efdetot * 100\n",
    "gen pctonline10 = pctonline/10\n",
    "gen logenrl = ln(ueftotlt)\n",
    "gen acceptrate = 100*admssn/applcn\n",
    "replace acceptrate = 100 if acceptrate==.\n",
    "gen totloan_a = floan_a+oloan_a\n",
    "foreach var in bbrr2_fed_ug_dflt bbrr2_fed_ug_dlnq bbrr2_fed_ug_fbr bbrr2_fed_ug_dfr bbrr2_fed_ug_noprog bbrr2_fed_ug_makeprog bbrr2_fed_ug_paidinfull bbrr2_fed_ug_discharge {\n",
    "\treplace `var' = \"\" if `var'==\"NULL\"\n",
    "\treplace `var' = \"\" if `var'==\"PrivacySuppressed\"\n",
    "\tdestring `var', replace\n",
    "\treplace `var' = 100*`var'\n",
    "\t\t}\n",
    "\t}\n",
    "\n",
    "*Save final analytic dataset\n",
    "save d_t3_t5_ipeds_scorecard, replace"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5a73a4c1-11bd-4729-abbf-53dd3b3cdac4",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Stata",
   "language": "stata",
   "name": "stata"
  },
  "language_info": {
   "codemirror_mode": "stata",
   "file_extension": ".do",
   "mimetype": "text/x-stata",
   "name": "stata",
   "version": "15.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
